//
// build.gradle in FtcRobotController
//
apply plugin: 'com.android.library'

android {

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        externalNativeBuild {
            cmake {
                cppFlags "-fexceptions"
            }
        }
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    compileSdkVersion 28

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: '../libs/librealsense', include: ['*.aar', '*.jar'], exclude: [])
}

repositories {
    flatDir {
        dirs '../libs'
    }
    maven{
        url "https://dl.bintray.com/intel-realsense/librealsense"
    }
}

configurations {
    downloadHeader
}

dependencies {
    def version = '2.+'
    downloadHeader 'com.intel.realsense:librealsense:' + version + '@zip'
}

task extractHeaders(type: Sync) {
    dependsOn configurations.downloadHeader
    from { configurations.downloadHeader.collect { zipTree(it) } }
    into "$projectDir/src/main/cpp/include"
}


task extractSo() {
    doLast {
        ConfigurableFileTree tree = fileTree(dir: '../libs/librealsense/jniLibs', include: '**/*.zip')
        tree.each { file ->
            copy {
                from(zipTree(file.canonicalFile))
                into file.parent
            }
        }

    }
}

preBuild.dependsOn(extractSo)
preBuild.dependsOn(extractHeaders)

apply from: 'build.release.gradle'
